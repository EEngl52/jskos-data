# Convert DDC in MARC21 Classification format to JSKOS

# Usage: catmandu convert marcxml --fix ddc-labels.fix to ndjson < ddc23en.xml > ddc23en-labels.ndjson

select marc_match(LDR/6, "w")   # record type == classification

# notation and uri
marc_map(153$ac, uri, join: "-")
if marc_has(153$z[0])
  # table record
  marc_spec(153$z[0],table)
  paste(uri,table,uri,join_char:"--")
  cp(uri,notation.0)
  prepend(notation.0,"T")
else  
  # main schedule record
  cp(uri,notation.0)
end
prepend(uri,"http://dewey.info/class/")
append(uri,"/e23/")

# created and modified
marc_map(008/0-5, created)
replace_all(created, '(\d\d)(\d\d)(\d\d)', '20$1-$2-$3')
marc_map(005/0-7, modified)
replace_all(modified, '(\d\d\d\d)(\d\d)(\d\d)', '$1-$2-$3')

# inScheme
add_field(inScheme.0.uri,"http://dewey.info/scheme/edition/e23/")
if exists(table)
  add_field(inScheme.1.uri, "http://dewey.info/table/")
  paste(inScheme.1.uri,inScheme.1.uri,table,join_char:"")
  append(inScheme.1.uri,"/e23/")
end

# broader and topConceptOf
marc_map(153$ef, parent, join: "-")
if exists(parent)
  if exists(table)
    paste(parent,table,parent,join_char:"--")
  end
  prepend(parent, "http://dewey.info/class/")
  append(parent,"/e23/")
  move_field(parent, broader.0.uri)
else

  # topConceptOf
  if all_match(notation.0,'^[0-9]$')
      copy_field(inScheme,topConceptOf)  
  end

  # TODO: table records lack broader information in the MARC data

end

# labels (FIXME: this also includes mappings)
# See <https://github.com/LibreCat/Catmandu-MARC/issues/107>.
marc_map(153$j, prefLabel.en)
marc_map(700$a,altLabel.en.$append)
marc_map(710$a,altLabel.en.$append)
marc_map(711$a,altLabel.en.$append)
marc_map(730$a,altLabel.en.$append)
marc_map(748$a,altLabel.en.$append)
marc_map(750$a,altLabel.en.$append)
marc_map(751$a,altLabel.en.$append)
marc_map(753$a,altLabel.en.$append)

unless exists(prefLabel)
  if exists(altLabel)
    mv(altLabel.en.$first, prefLabel.en)
  end
end

rm(table)
rm(record)
rm(_id)
